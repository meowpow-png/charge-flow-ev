//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.avast.gradle.docker-compose' version '0.17.21'
}

group = 'io.github.meowpowpng'
version = '0.0.1-SNAPSHOT'
description = 'Charging Operations Platform'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

dockerCompose {
    useComposeFiles = [rootProject.file('docker-compose.yml').path]
    startedServices = ["postgres", "adminer"]

    removeVolumes = true
    removeOrphans = true

    isRequiredBy(tasks.named("bootRun"))
}

repositories {
	mavenCentral()
}

sourceSets {
    integrationTest {
        java.srcDir 'src/integrationTest/java'

        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += sourceSets.main.output + output + compileClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'

    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    integrationTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation 'org.testcontainers:postgresql:1.21.3'
    integrationTestImplementation 'org.testcontainers:jdbc:1.21.3'
}

tasks.named("bootRun") {
    dependsOn composeUp
    finalizedBy composeDown
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register('integrationTest', Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    shouldRunAfter test
}

tasks.named('check') {
    dependsOn integrationTest
}

tasks.register('jacocoMergedReport', JacocoReport) {
    dependsOn tasks.named('test')
    dependsOn tasks.named('integrationTest')

    executionData fileTree(layout.buildDirectory).include(
            "jacoco/test.exec",
            "jacoco/integrationTest.exec"
    )
    sourceSets sourceSets.main

    reports {
        xml.required = true
        html.required = true
    }
}
